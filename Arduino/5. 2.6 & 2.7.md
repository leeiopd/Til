# Arduino_ArduinoCookbook

## day4_2019-06-29

### 2.6 C문자열 사용하기

> 원시 문자열을 사용하는 방법
>
> * 문자열 생성 방법
> * 길이를 알아내는 방법
> * 문자열을 비교, 복사 또는 추가하는 방법
> * 아두이노 스타일의 String이 지원되지 않는 코어 C 언어에서 원시 문자 배열의 처리 방법

```c++
char stringA[8]; // 최대 7개의 문자를 포함하고 널로 끝나는 문자열을 선언
char StringB[8] = "Arduino"; // 위와 같은 문자열을 선언한 후 문자열을 "Arduino"로 초기화

char stringC[16] = "Arduino"; // 위와 같지만 문자열에 여유 공간이 있다.
char stringD [] = "Arduino"; // 컴파일러가 문자열을 초기화하고 크기를 계산한다.

int length = strlen(string); // 문자열의 문자 수를 리턴한다.
strcpy(destination, source); // source 문자열을 destination 문자열에 복사한다.
strncpy(destination, source, 6); // 최대 6개의 문자를 sourceㅔ서 detinatnion으로 복사한다.
strcat(destinatnion, source); // source 문자열을 destination 문자열의 끝에 추가한다.

if (strcmp(str, "Arduino") == 0 )
  // str 변수가 "Arduino"와 같으면 작업을 수행한다.
```



### 2.7 쉼표로 구분된 텍스트를 그룹으로 분리하기

> 쉼표로 구분된 두 가지 이상의 데이터가 포함된 문자열의 분리

```c
/*
 * SplitSplit 스케치
 * 쉼표로 구분된 문자열을 분리한다.
 */

String text = "Peter, Paul, Mary"; // 예제 문자열
String message = text; // 아직 분리되지 않은 문자열을 담고 있다.
int commaPosition; // 문자열에 있는 다음 쉼표의 위치

void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);

  Serial.println(message); // 소스 문자열을 표시한다.
  do // do-while : 코드를 일다 실행하고, 조건에 따라 반복
     // 이 코드에서는 while문의 반복 조건인 commaPosition >= 0 선언을 do에서 해준 뒤 조건에 따라 반복이기 때문에 do-while 을 사용
  {
    commaPosition = message.indexOf(','); // ','의 index num을 commaPositon에 저장
      									  // ','가 message에 없으면 -1
      
    if (commaPosition != -1)
    {
      Serial.println(message.substring(0, commaPosition)); 
      // message의 첫번째 글자부터 commpaPosition 앞 까지의 문자열을 인쇄
      
      message = message.substring(commaPosition+1, message.length());
      // message에 commaPosition 다음 index 글자 부터 남은 message를 다시 저장
      // (출력된 문자열과 ',' 다음부터 저장)
    }
    
    else
    // commaPosition == -1 message에 ','가 없는 경우
    {
      // 마지막 쉼표 분리 이후
      if(message.length() > 0) // message의 문자열이 존재하는 경우
        Serial.println(message); // 마지막 쉼표 이후에 텍스트가 있으면 해당 텍스트를 인쇄한다.
    }
  }
  while(commaPosition >= 0); // ','가 message에 존재 할 경우

}

void loop() {
  // put your main code here, to run repeatedly:

}
```

* `commaPosition = message.indexOf(',');`
  * commaPosition 변수를 message 문자열에 있는 첫 번째 쉼표의 위치로 설정
  * (쉼표가 없으면 -1로 설정)
* `substring` 함수
  * 문자열의 처음부터 쉼표 앞까지의 텍스트를 인쇄

* `message = message.substring(commaPosition+1, message.length());`
  * 인쇄된 텍스트와 뒤따르는 쉼표가 message에서 제거 됨
  * `substring 함수`는 `commaPosition+1`(첫번째 쉼표 바로 뒤의 위치)부터 시작해서 메시지 길이만큼 문자열을 리턴한다.
  * 첫번째 쉼표 이후의 텍스트만 포함된 메시지가 만들어짐
* message의 쉼표가 더이상 없을 때 까지 반복 (commaPosition이 -1이 된다)





```c++
// C 라이브러리의 하위 레벨 함수 사용
// C 라이브러리 하위 레벨 함수 사용

/*
 * SplitSplit 스케치
 * 쉼표로 구분된 문자열을 분리한다.
 */

const int MAX_STRING_LEN = 20; // 앞으로 처리할 문자열 중 가장 긴 문자열로 설정.
                               // const로 선언하면 내용 불변
                               
char stringList[] = "Peter, Paul, Mary"; // 예제 문자열
char stringBuffer[MAX_STRING_LEN+1]; // 계산 및 출력을 위한 정적 버퍼
 
void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
}

void loop() {
  // put your main code here, to run repeatedly:
  char *str;
  char *p;
  strncpy(stringBuffer, stringList, MAX_STRING_LEN); // 소스 문자열을 복사한다.
  Serial.println(stringBuffer); // 소스 문자열을 표시한다.

  for(str = strtok_r(stringBuffer, ", ", &p); // 쉼표를 사용하여 분리한다.
    str; // str이 널이 아닐 때까지 반복한다.
    str = strtok_r(NULL, ", ", &p)) // 후속 토큰을 가져온다.
    {
      Serial.println(str);
    }
    delay(5000);
}
```

* `strtok_r 함수`
  * 아두이노 컴파일러에서제공하는 `strtok`함수의 이름
  * strtok_r 함수를 처음 호출할 때 토큰을 사용하여 구분할(개별 값으로 분리할) 문자열을 매개변수로 전달한다.
  * strtok_r은 새 토큰을 찾을 때마다 문자열에 있는 문자열을 덮어 쓰므로 문자열의 사본을 전달하는 것이 좋다.
  * 이후 호출에선느 첫 번째 매개변수로 NULL을 지정하여 다음 토큰으로 이동하도록 지시한다.
  * 이 예제에서는 각 토큰이 시리얼 포트에 인쇄된다.